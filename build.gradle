plugins {
    id 'fabric-loom' version '1.1.7'
    id 'io.github.juuxel.loom-quiltflower' version '1.10.0'
    id "com.modrinth.minotaur" version "2.+"
}

version = "${project.mod_version}+${project.minecraft_version}"

repositories {
    maven { url "https://maven.shedaniel.me/"}
    maven { url "https://maven.terraformersmc.com/releases/"}
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}"
    modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

processResources {
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand(
                "minecraft_version": project.minecraft_version,
                "mod_version": project.mod_version,
                "loader_version": project.loader_version,
                "cloth_version": project.cloth_version,
                "mod_menu_version": project.mod_menu_version
        )
    }
}


tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    def javaVersion = JavaVersion.toVersion(17)

    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(17)
    }

    archivesBaseName = project.archives_base_name
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "HmJkmcXO"
    versionNumber = project.mod_version
    versionName = "${project.mod_version} for ${project.minecraft_version}"
    versionType = "release"
    changelog = "Automatically uploaded with Minotaur. Changelog available on [GitHub releases](https://github.com/Erb3/NoHotbarLooping/releases/tag/${project.mod_version}-${project.minecraft_version})."
    gameVersions = [project.minecraft_version]
    loaders = ["fabric"]
    uploadFile = remapJar
    syncBodyFrom = rootProject.file("README.md").text

    dependencies {
        required.project "fabric-api"
        optional.project "modmenu"
        optional.project "cloth-config"
    }
}
tasks.modrinth.dependsOn(tasks.modrinthSyncBody)